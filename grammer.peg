Expression
  = Definition

Definition
  = left:Symbol _ ":=" _ right:Equation _ {
    return {
      type: "definition",
      elements: [left, right]
    };
  }
  / Equation

Equation
  = left:List "=" right:List _ {
    return {
      type: "equation",
      elements: [left, right]
    };
  }
  / List

List
  = _ "[" first:Expression following:ListFollowingElement* "]" _{
    var node = {type: "list", elements: []};
    if (first != null) {
      node.elements.push(first);
    }
    for (var i = 0; i < following.length; i++) {
      node.elements.push(following[i]);
    }
    return node;
  }
  / Sum

ListFollowingElement
  = _ "," _ expr:Expression _ {return expr;}

Sum
  = unsigned:unsignedSummand? _ signed:signedSummand+ {
  	var node = {type: "sum", elements: []};
    if (unsigned != null) {
      node.elements.push(unsigned);
    }
    for (var i = 0; i < signed.length; i++) {
      node.elements.push(signed[i]);
    }
    return node;
  }
  / Product

signedSummand
  = _ sign:("+" / "-") _ expr:Product _ {
    var nExpr = expr;
    nExpr.sign = sign;
    return nExpr;
  }

unsignedSummand
  = _ expr:Product _ {
    var nExpr = expr;
    nExpr.sign = "+";
    return nExpr;
  }

Product
  = unsigned:unsignedFactor _ signed:signedFactor+ {
    var node = {type: "product", elements: []};
    if (unsigned != null) {
      node.elements.push(unsigned);
    }
    for (var i = 0; i < signed.length; i++) {
      node.elements.push(signed[i]);
    }
    return node;
  }
  / Power

signedFactor
  = _ sign:("*" / "/") _ expr:Power _ {
    var nExpr = expr;
    nExpr.mulSign = sign;
    return nExpr;
  }

unsignedFactor
  = _ expr:Power _ {
    var nExpr = expr;
    nExpr.mulSign = "*";
    return nExpr;
  }

Power
  = base:Function "^" exp:Function _ {
    return {
      type: "power",
      base: base,
      exp: exp
    };
  }
  / Function

Function
  = name:Symbol "(" first:FirstParam following:FollowingParam* ")" _ {
    var node = {type: "function", name: name.value, elements: []};
    if (first != null) {
      node.elements.push(first);
    }
    for (var i = 0; i < following.length; i++) {
      node.elements.push(following[i]);
    }
    return node;
  }
  / Primary

FirstParam
  = _ expr:Expression _ {return expr;}

FollowingParam
  = _ "," _ expr:Expression _ {return expr;}

Primary
  = "(" _ expr:Expression _ ")" {return expr;}
  / number:Number _ {return number;}
  / symbol:Symbol _ {return symbol;}

Number "integer"
  = _ [0-9]+ ("." [0-9]*)?  {
    return {
      type: "number",
      value: parseFloat(text(), 10)
    };
  }

Symbol
  = [a-zA-z_]+ {
    return {
      type: "symbol",
      value: text()
    }
  }

_ "whitespace"
  = [ \t\n\r]*
